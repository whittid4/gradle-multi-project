buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.2.1'
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'jacoco'  // This needs to be applied at this level for some reason
    apply plugin: 'coveralls'

    repositories {
        mavenCentral()
    }

    task hello << { task -> println "I'm $task.project.name" }

}

subprojects {
    apply plugin: 'java'

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    // Turn off reporting for individual projects. If not turned off aggregate report fails
    test {
        reports.html.enabled = false
    }
}

project(':app') {
    apply plugin: 'war'
    apply plugin: 'jetty'       // Embedded Jetty is preferred

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.10'
        compile project(':common')
    }

    jettyRun {
        httpPort = 9000
    }
}

project(':app2') {
    apply plugin: 'war'
    apply plugin: 'jetty'       // Embedded Jetty is preferred

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.10'
        compile project(':common')
    }

    jettyRun {
        httpPort = 9001
    }
}

task testReport(type: TestReport) {
    group = 'reports'
    description = 'Creates a merged test report'

    destinationDir = file("$buildDir/reports/tests")

    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task coverageReport(type: JacocoReport) {
    group = 'reports'
    description = 'Creates a merged coverage report'

    dependsOn = subprojects.test

    // Find the execution data from all subprojects
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        html.enabled true
        html.destination "${buildDir}/reports/jacoco/test/html"
        csv.enabled false
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
